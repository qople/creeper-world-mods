# Big ol' gun.

$shot_damage:25.0 # 25
$shot_damage_count:9999
$shot_damage_distance:14
$fire_delay:300  # 10 seconds
$FIRE_RANGE:100
$FIRE_COST:10

# Remade rocket module, NO damage map since that doesn't exist as of when this was written.

# Change RA range when the variable is updated mid-level
if(<FIRE_RANGE <lastRange neq)
    Self <FIRE_RANGE SetRaRange
    Self <FIRE_RANGE SetShowRange
    <FIRE_RANGE >lastRange
endif

# Everything below only happens while the part is active and healthy
Sys_Operable

if(GetTimer0 eq0)
    #if(Self GetAmmo <FIRE_COST gte)
        @Fire >fireSuccess
        if(<fireSuccess)
            <FIRE_DELAY SetTimer0
        else
            #Short timer if we don't find creeper, but not 0 to help avoid lag when no creeper is near.
            10 <FIRE_DELAY min SetTimer0
        endif
    #endif
endif

:Fire
    GetDeepestCreeper(Self false true) >position
    if(<position.x gte0)
        # Create the unit
        "/ApexEvolution/Modules/Bertha/berthashot" Self GetPosition true CreateUnit >shot
        PlaySound("retro_weapon_gun_misc_shot_08" 0.5)
        Self <position GetRaPath >pathlist
        # Set variables on the shot
        <shotVars 0 do
            <shotVars[i] >varName
            <shot <varName dup <-! SetScript1Var
        loop
        true return
    else
        false return
    endif

:Once
    #Vars in this list get copied to the bullet when we make it
    List("pathList" "shot_damage_count" "shot_damage_distance" "shot_damage") >shotVars
    
:Awake
    Self true EnableRa
    Self <FIRE_RANGE SetShowRange
    Self V4(0.8 0 0.4 0.8) SetShowRangeColor
    Self <FIRE_RANGE SetRaRange
    <FIRE_RANGE >lastRange
