# The first Legendary-class weapon in AEV. Really just a bigger cannon.
$fire_range:80
$shot_damage:500.0
$shot_damage_radius:25
$shot_damage_count:99999
$fire_delay:300


$ROTATE_SPEED:1.0
$FIRE_COST:15

$num_shots:1
$shot_speed:2.0
$spread:20

$shrapnel:0

if(<fire_range <lastRange neq)
    SetLosRange(Self <fire_range)
    SetShowRange(Self <fire_range)
    <fire_range >lastRange
endif

SYS_Operable

if(GetTimer0 30 lt)
    GetNearestCreeper(self false) ->nearestCreeper
    if (<-nearestCreeper.x gte0)
        <-nearestCreeper ->rotateTarget
        SignedAngle(GetDirRight(self) <-rotateTarget GetPosition(self) -) ->rotationRemaining
        <timeTurning 1 add >timeTurning
        if (abs(<-rotationRemaining) <ROTATE_SPEED <timeTurning 0.01 mul 1 add mul <)
            <-nearestCreeper ->hardTarget
            if (GetTimer0 eq0) 
                if (GetAmmo(self) <-FIRE_COST >=)
                    @TryFire
                    0 >timeTurning
                endif	
            endif	
        else
            Rotate(self clamp(<-rotationRemaining neg(<-ROTATE_SPEED) <-ROTATE_SPEED))
            V2(-1 -1) ->hardTarget
        endif
    endif
endif

#---
:TryFire
	GetCreeperOnLine(self dup GetRotation 100 <-hardTarget false) ->col
	if (<-col.x gte0)
        PlaySound("retro_explosion_general_16" 1.0)
        Self GetRotation <num_shots 1 sub <spread 0.5 mul mul sub >rot
        <num_shots 0 do
            CreateUnit("/ApexEvolution/Modules/OdinCannon/odincannonshot" Self GetPosition Self GetDirRight 20 mul add true) >shot
            <shotVars 0 do
                <shotVars[i] >varName
                <shot <varName dup <-! SetScript1Var
            loop
            <shot <rot SetRotation
            <rot <spread add >rot
        loop
        <fire_delay RandFloat add SetTimer0
        ConsumeAmmo(Self 0.5)
	endif
#---

:Once
    List("parent", "shot_damage_radius" "shot_damage", "shot_damage_count", "shot_speed", "shrapnel") >shotVars
    <fire_delay SetTimer0
    Self >parent

:Awake
    0 ->lastRange
	EnableLOS(self true)
    SetLosRange(Self <fire_range)
    SetShowRange(Self <fire_range)
    SetShowRangeColor(Self 0.8 0 0.388 0.8 V4)