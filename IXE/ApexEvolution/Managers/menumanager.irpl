# Controls mid-game menu interaction

if(<showIndicator GetEditMode ! && GetGameUpdateCount eq0 &&)
    ShowUiIndicator("AevHelp" @GetIndicatorPosition)
endif

# Update the stats UI after each upgrade
if(<updateUi)
    DestroyUI("StatsMenu")
    SendMsg("ShowStatsMenu" 0)
    if(<openModule StringLength)
        SetUIBackgroundColor("StatsMenu" <module V4(0 0.8 0 1))
        DestroyUI("AevModule")
        SendMsg("ShowModuleUi" <openModule)
    endif
    false >updateUi
endif

if(GetFreeBuild)
    once
        if(<openMenu "main" eq)
            # Close and reopen the menu to show the new tabs
            SendMsg("SHOW_AEV_MENU" 0)
            SendMsg("SHOW_AEV_MENU" 0)
        endif
    endonce
endif

:ToggleAEVMenu
    false >showIndicator
    "" >openModule
    if(<openMenu "stats" eq)
        DestroyUi("StatsMenu")
        DestroyUI("AevModule")
        SetUiBackgroundColor("AevMenu" "stats" V4(0 0.204 0.267 1))
    endif
    if(<openMenu "main" eq)
        SendMsg("CloseMainMenu" 0)
        "" >openMenu
        SetUiBackgroundColor("AevMenu" "main" V4(0 0.204 0.267 1))
    else
        "main" >openMenu
        SendMsg("ShowMainMenu" 0)
        SetUiBackgroundColor("AevMenu" "main" V4(0 0.8 0 1))
    endif
    
:ToggleStatsMenu
    false >showIndicator
    "" >openModule
    if(<openMenu "main" eq)
        SendMsg("CloseMainMenu" 0)
        SetUiBackgroundColor("AevMenu" "main" V4(0 0.204 0.267 1))
    endif
    if(<openMenu "stats" eq)
        DestroyUI("StatsMenu")
        DestroyUI("AevModule")
        "" >openMenu
        SetUiBackgroundColor("AevMenu" "stats" V4(0 0.204 0.267 1))
    else
        "stats" >openMenu
        SendMsg("ShowStatsMenu" 0)
        SetUiBackgroundColor("AevMenu" "stats" V4(0 0.8 0 1))
    endif

:SHOW_MODULE_STATS
    <_DATA{"name"} >module
    if(<openModule StringLength)
        DestroyUI("AevModule")
        SetUIBackgroundColor("StatsMenu" <openModule V4(0 0.204 0.267 1))

    endif
    if(<module <openModule neq)
        <module >openModule
        if(<module StringLength)
            SetUIBackgroundColor("StatsMenu" <module V4(0 0.8 0 1))
            SendMsg("ShowModuleUi" <openModule)
        endif
    else
        "" >openModule
    endif

:UpdateStatMenu
    if(<openMenu "stats" eq)
        1 >updateUi
    endif

:GetIndicatorPosition
    0 GetScreenSize [1] V2 >topLeftCorner
    <topLeftCorner V2(46 -57) @GetUiScale mul add

:Once
    "" >openMenu
    "" >openModule
    true >showIndicator
    RegisterForMsg("SHOW_AEV_MENU" "ToggleAEVMenu")
    RegisterForMsg("SHOW_AEV_STATS" "ToggleStatsMenu")
    RegisterForMsg("SHOW_MODULE_STATS" dup)
    RegisterForMsg("ApplyUpgrade" "UpdateStatMenu")

# Calculates the current UI scale
:GetUiScale
    GetScreenSize GetPositionHelpButton sub V2(52 16) div [0]