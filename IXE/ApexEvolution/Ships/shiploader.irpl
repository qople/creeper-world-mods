# Does the heavy lifting of loading ships from the ship irpl template files. 
# Makes the template cleaner, and removes all this code that shouldn't be modified when adding in a ship.

:Once
    RegisterForMsg("LoadShip" dup)
    false >loaded
    false ->*shipLoaded
    CreateTable ->*moduleBehaviors
    CreateTable ->*unlockedModules
    "shot_damage" "shot_damage_count" "shot_damage_distance" "shot_damage_radius" "fire_delay" List >requiredModuleStats

:LoadShip
    # Workaround for bug in version 39 that causes messages to be received more than once in certain situations.
    if(<loaded)
        return
    endif
    once
        true >loaded
        true ->*shipLoaded
    endonce


    #CreateTable ->*allModules

    "mainShip" @Get dup >mainShip ->*mainShip

    # First get the custom modules on the ship
    "custom_modules" @Get RemoveWhiteSpace ToLower "," Split >customModules
    <-*allModules GetTableKeys Copy >shipModules
    <customModules 0 do
        if(<customModules[i] StringLength)
            <shipModules <customModules[i] AppendToList
        endif
    loop

    #<shipModules ->*testModules
    <shipModules 0 do
        <shipModules[i] @LoadModule
    loop

    # Add ship upgrades to the pool
    SendMsg("AddUpgradesToPool" "_upgrades" @Get)

:LoadModule
    >moduleName

    # Get data on total and active modules and set it up
    <moduleName @DetectModuleNum >numModules
    if(<numModules IsNull)
        0 >numModules
    endif

    if(<numModules)
        <numModules ->*allModules{<moduleName}
        <mainShip "ship.irpl" "num_" <moduleName concat <numModules SetScriptVar 

        "start_unlocked_" <moduleName concat @Get >startingModules
        if(<startingModules IsNull)
            0 >startingModules
        endif

        <startingModules ->*unlockedModules{<moduleName} 
        <numModules 0 do
            <mainShip <moduleName i concat GetChild i <startingModules lt SetActive
        loop


        # Figure out the setup mode
        #<baseModules <moduleName ListContains >isBaseModule

        if("setup_mode_" <moduleName concat @Get IsNull !)
            # They defined a setup mode, let's use that.
            "setup_mode_" <moduleName concat @Get >setupMode
        else
            #Default to automatic setup.
            0 >setupMode
        endif



        if(<setupMode eq0)
            <moduleName @AutomaticSetup
        else if(<setupMode 1 eq)
            <moduleName @ManualSetup
        endif endif

        # Make the table for the module if it doesn't exist
        if(<-*moduleStats <moduleName TableContainsKey !)
            CreateTable ->*moduleStats{<moduleName}
        endif

        <requiredModuleStats 0 do
            <requiredModuleStats[i] >attribute
            if(<-*moduleStats{<moduleName} <attribute TableContainsKey !)
                <-*moduleStats{<moduleName} <attribute @GetStat(<moduleName <attribute) 0.0 1.0 1.0 4 ListN SetTableElement
            endif
        loop
    else
        <-*allModules <moduleName RemoveTableElement
    endif

# Gets the value of the given stat for a module
:GetStat
    >attribute >module

    <-*moduleBehaviors{<module} >behavior
    <behavior ".irpl" EndsWith  <behavior StringLength eq0 || >useScript

    if(<useScript)
        <-*mainShip <module "0" concat GetChild >moduleUnit
        <moduleUnit <behavior <attribute GetScriptVar return
    else
        # Controller instead
        # GetControllerArg is odd, so we'll do something weird
        #StackSize >size
        #<-*mainShip <behavior <attribute GetControllerArg TraceStack
        #StackSize <size sub >numArgs
        #<numArgs 1 sub 0 do
        #    pop
        #loop

        <-*mainShip <module "0" concat GetChild 0 <attribute GetControllerArg2
        return
    endif

# Loads the default upgrades for the module 
:AutomaticSetup
    >moduleName
    SendMsg("add" <moduleName concat <startingModules)
    
:ManualSetup
    # Nothing right now, this is here in case something needs to be done here later.
    pop
    
# Shorthand function
:Get
    "ship" swap GetRegisteredScriptVar

# Shorthand function
:Set
    "ship" swap SetRegisteredScriptVar

:DetectModuleNum
    >moduleName
    999 0 do
        if(<mainShip <moduleName i concat GetChild -1 eq)
            i return
        endif
    loop
    999 return